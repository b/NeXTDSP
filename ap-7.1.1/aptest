#!/bin/csh -f

# Makes ap tests and examples then invokes aptest_aux tee'ing output to results file
# Options:
# 	-c	DON'T do make clean
# 	-l	Make local version - links with clone ap lib instead of
#		installed ap lib

set cflag=0 lflag=0
while ( $#argv > 0 )
	if ( "$argv[1]" !~ -* ) break
	if ( "$argv[1]" =~ *c* ) then
		set cflag=1
	else if ( "$argv[1]" =~ *l* ) then
		set lflag=1
	else
		break
	endif
	shift
end
if ( $#argv > 0 ) then
	echo Usage: $0 \[-c\] \[-l\] >/dev/tty
	exit(1)
endif

set make_options=
unsetenv DSP
set results=aptest_results
echo === Testing Array Processing Library ===
echo === Results in $results ===
if (-e $results) \
	/bin/mv $results $results.previous

# make examples and tests
cd src/bin
if ( $cflag ) then
	echo === NOT cleaning before make ===
else
	echo === make clean ===
	make clean
endif
if ( $lflag ) \
	set make_options=local_install
echo === make $make_options ===
make $make_options

cd ../..
aptest_aux $1 | tee $results

if (-e $results.previous) then
	echo === diff $results $results.previous ===
	diff $results $results.previous
else
	echo === No previous results to diff ===
endif
